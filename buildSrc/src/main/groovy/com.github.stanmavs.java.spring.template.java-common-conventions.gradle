plugins {
    // Apply the java Plugin to add support for Java.
    id 'java'
    id 'jvm-test-suite'
    id "com.diffplug.spotless"
}

repositories {
    // Use Maven Central for resolving dependencies.
    mavenCentral()
}

ext {
    lombokVersion = "1.18.24"
    mapstructVersion = "1.5.3.Final"
}

dependencies {
    compileOnly "org.projectlombok:lombok:$lombokVersion"
    annotationProcessor "org.projectlombok:lombok:$lombokVersion"
    compileOnly "org.mapstruct:mapstruct:$mapstructVersion"
    annotationProcessor "org.mapstruct:mapstruct-processor:$mapstructVersion"
}

spotless {
    java {
        googleJavaFormat('1.15.0')
        // fix formatting of type annotations
        formatAnnotations()
        toggleOffOn()
    }
}

testing {
    suites {
        test {
            useJUnitJupiter()

            dependencies {
                compileOnly "org.projectlombok:lombok:$lombokVersion"
                annotationProcessor "org.projectlombok:lombok:$lombokVersion"
                compileOnly 'org.mockito:mockito-core:5.0.0'
                implementation 'org.mockito:mockito-junit-jupiter:4.6.1'
            }
        }

        integrationTest(JvmTestSuite) {
            dependencies {
                implementation project()
                compileOnly "org.projectlombok:lombok:$lombokVersion"
                annotationProcessor "org.projectlombok:lombok:$lombokVersion"
                implementation "org.springframework.boot:spring-boot-starter-test"
                implementation "io.rest-assured:rest-assured:5.3.0"
            }

            targets {
                all {
                    testTask.configure {
                        shouldRunAfter(test)
                    }
                }
            }
        }
    }
}

tasks.named('check') {
    dependsOn(testing.suites.integrationTest)
}